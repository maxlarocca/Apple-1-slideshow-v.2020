/*
 * Apple I ASCII art slideshow.
 */

#define	SLIDEPTR	$06
#define	CDELAY	50
#define	MONITOR		$FF00
#define	COUT		$FFEF

	*=$0280
ENTRY	LDA	#>MONITOR
	PHA
	LDA	#<MONITOR
	PHA
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
FRSTSLD	LDA	#<SLIDES
	STA	SLIDEPTR
	LDA	#>SLIDES
	STA	SLIDEPTR+1
SHOWSLD	JSR	PRCR
SLOOP	LDY	#$00
	LDA	(SLIDEPTR),Y
	BEQ	PRTTL
	LSR
	LSR
	LSR
	LSR
	TAX
	LDA	(SLIDEPTR),Y
	AND	#$0F
	TAY
	LDA	GREY2ASCII,Y
RLDEC	JSR	COUT
	DEX
	BNE	RLDEC
	INC	SLIDEPTR
	BNE	SLOOP
	INC	SLIDEPTR+1
	BNE	SLOOP
PRTTL	INY
	SEC
	LDA	#40
	SBC	(SLIDEPTR),Y
	LSR
	TAX
	LDA	#$A0
SPCS	JSR	COUT
	DEX
	BNE	SPCS
	LDA	(SLIDEPTR),Y
	TAX
TLOOP	INY
	LDA	(SLIDEPTR),Y
	JSR	COUT
	DEX
	BNE	TLOOP
	INY
	TYA
	CLC
	ADC	SLIDEPTR
	STA	SLIDEPTR
	LDA	#$00
	ADC	SLIDEPTR+1
	STA	SLIDEPTR+1
	LDA	#$10
	JSR	GETKYWT
	JSR	PRCR
	LDY	#$00
	LDA	(SLIDEPTR),Y
	BEQ	FRSTSLD
	BNE	SHOWSLD
GETKYWT	PHA
	TXA
	PHA
	TYA
	PHA
	LDY	#$FF
GKW1	LDX	#$FF
GKW2	LDA	$D011
	BPL	GKW3	
	PLA
	TAY
	PLA
	TAX
	PLA
	LDA	$D010
	RTS	
GKW3	DEX
	BNE	GKW2
	DEY
	BNE	GKW1
	PLA
	TAY
	PLA
	TAX
	PLA
	SEC
	SBC	#$01
	BNE	GETKYWT
	LDA	#$00
	RTS	
PRCR	PHA
	LDA	#$8D
	JSR	COUT
	PLA
	RTS
	
GREY2ASCII
	.BYT	$A0, $AE, $BA, $AC, $BB, $A1, $AD, $DE
	.BYT	$AB, $BD, $BF, $A6, $AA, $A5, $A3, $C0
	/*.ASC	"  .:,;!-^+=?&*%#@" */
SLIDES
#include "40x23_8bitgray_jobs2011.rle"
	.BYT	23
	.ASC	"STEVE JOBS (1955-2011) "
#include "40x23_8bitgray_woz.rle"
	.BYT	14
	.ASC	"STEVE WOZNIAK "
#include "applei.rle"
	.BYT	38
	.ASC	"THE TWO STEVES HOLDING APPLE I (1976) "
#include "appleii.rle"
	.BYT	16
	.ASC	"APPLE II (1977) "
#include "mac128k.rle"
	.BYT	17
	.ASC	"MACINTOSH (1984) "
#include "imac.rle"
	.BYT	12
	.ASC	"IMAC (1998) "
#include "ipod.rle"
	.BYT	12
	.ASC	"IPOD (2001) "
#include "40x23_8bitgray_iphone.rle"
	.BYT	14
	.ASC	"IPHONE (2007) "
#include "40x23_8bitgray_ipad.rle"
	.BYT	12
	.ASC	"IPAD (2010) "
#include "40x23_8bitgray_applewatch.rle"
	.BYT	19
	.ASC	"APPLE WATCH (2014) "
#include "40x23_8bitgray_logo_new.rle"
	.BYT	11
	.ASC	"APPLE LOGO "
	.BYT	0
